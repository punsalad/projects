#!/usr/bin/perl
#
# booklister -- output TBR book stacks in HTML
#

use strict;
use warnings;
use English qw( -no_match_vars );
use HTML::Template;
use Time::Piece;
use Time::Seconds;
use open qw(:std :utf8);
use version; our $VERSION = qv('v2022.04.15');

my $now       = localtime;
my $stackfile = "$ENV{'HOME'}/var/bookstacks.pl";

my $template_file = $ENV{'HOME'} . '/Templates/booklister.tmpl';
my $template      = HTML::Template->new( filename => $template_file, );

#
# read stack data...
#
our (@STACKS);
if ( !( my $return = do $stackfile ) ) {
    die "Couldn't parse $stackfile $EVAL_ERROR\n" if $EVAL_ERROR;
    die "Couldn't do    $stackfile $ERRNO\n"      if !defined $return;
    die "Couldn't run   $stackfile\n"             if !$return;
}

#
# populate template
#
my @stackloop  = ();
my @weightloop = ();

for my $stack (@STACKS) {
    my $lastpicked_lt
        = Time::Piece->strptime( $stack->{lastpicked}, '%Y-%m-%d' );
    my $stack_age = $now - $lastpicked_lt;
    my $eligible
        = (    $stack_age->days >= $stack->{minage}
            && scalar @{ $stack->{books} } > 0
            && $stack->{books}->[0]{owned} );
    $eligible && push @weightloop,
        {
        ename  => $stack->{name},
        weight => int( $stack_age->days * scalar @{ $stack->{books} } ),
        };

    my @titleloop = ();
    for my $book ( @{ $stack->{books} } ) {
        push @titleloop,
            {
            title  => $book->{title},
            author => $book->{author},
            asin   => $book->{ASIN},
            owned  => $book->{owned},
            };
    }

    push @stackloop,
        {
        name     => $stack->{name},
        minage   => $stack->{minage},
        age      => sprintf( '%d', $stack_age->days ),
        eligible => $eligible,
        titles   => \@titleloop,
        };
}
$template->param( 'stacks'  => \@stackloop );
$template->param( 'weights' => \@weightloop );

#
# write to tmp
#
my $htmlfile = '/tmp/booklist.html';
open my $F, '>', $htmlfile
    or die "Failed to open $htmlfile: $ERRNO\n";
print {$F} $template->output;
close $F or warn "Closing $htmlfile failed: $ERRNO\n";
print "HTML booklist saved at file://$htmlfile\n";
