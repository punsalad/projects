#!/usr/bin/perl
#
# output HTML link of page displayed in Chrome active tab
#

use strict;
use warnings;
use Const::Fast;
use Encode qw(encode_utf8);
use English qw( -no_match_vars );
use JSON;
use open qw(:std :utf8);
use utf8;
use version; our $VERSION = qv('v2021.04.06');

const my $CLIENT => '/usr/local/bin/chromix-too';

#
# assume Chromix server is running; get active tab in current window
#
open my $CTQ, q{-|},
    $CLIENT
    . q{ raw chrome.tabs.query '{ "active":true, "currentWindow":true }'}
    or die "Failed to run tab query: $ERRNO\n";
my $ctq_json = <$CTQ>;
close $CTQ or warn "Closing tab query failed: $ERRNO\n";
my $tab_aref = decode_json(encode_utf8($ctq_json));

#
# the query should only give us one tab, so pop it;
#
my $tab = pop @{$tab_aref};

#
# for target text, first get any input (trim trailing/leading whitespace)
#
my $t
    = do { local $RS = undef; <STDIN> };  ## no critic 'ProhibitExplicitStdin'
$t =~ s/^ \s+ | \s+ $//gx;

#
# if no input ...
#
if ( !$t ) {
    #
    # use page title
    #
    $t = $tab->{title};
    #
    # chop off (probable) site identity at title end...
    #
    $t =~ s{\s+  (
			[\N{RIGHT-POINTING DOUBLE ANGLE QUOTATION MARK}
			\N{LEFT-POINTING DOUBLE ANGLE QUOTATION MARK}
			\N{VERTICAL LINE}
			\N{HYPHEN-MINUS}
			\N{EN DASH}
			\N{EM DASH}]|
			\N{COLON}\N{COLON}|
			\N{HYPHEN-MINUS}\N{HYPHEN-MINUS}
		) \s+ .* $}{}x;
}

printf "<a href=\"%s\">%s</a>\n", $tab->{url}, $t;
